component {

    variables.service = 'personalize-events';

    public any function init(
        required any api,
        required struct settings
    ) {
        variables.api = arguments.api;
        variables.apiVersion = arguments.settings.apiVersion;
        return this;
    }

    /**
    * Records user interaction event data.
    * https://docs.aws.amazon.com/personalize/latest/dg/API_UBS_PutEvents.html
    * @eventList required array: A list of event data from the session. Array of Event objects (https://docs.aws.amazon.com/personalize/latest/dg/API_UBS_Event.html)
    * @sessionId required string: The session ID associated with the user's visit.
    * @trackingId required string: The tracking ID for the event. The ID is generated by a call to the CreateEventTracker API.
    * @userId required string: The user associated with the event.
    */
    public any function putEvents(
        required array eventList,
        required string sessionId,
        required string trackingId,
        required string userId
    ) {
        var requestSettings = api.resolveRequestSettings( argumentCollection = arguments );
        var args = {
            'eventList': arguments.eventList,
            'sessionId': arguments.sessionId,
            'trackingId': arguments.trackingId,
            'userId': arguments.userId
        };

        return apiCall(
            requestSettings,
            'PutEvents',
            '/events',
            args
        );
    }

    private any function apiCall(
        required struct requestSettings,
        required string target,
        required string path,
        struct payload = { }
    ) {
        var host = variables.service & '.' & requestSettings.region & '.amazonaws.com';

        var payloadString = serializeJSON( payload );
        var headers = { };
        headers[ 'Content-Type' ] = 'application/x-amz-json-1.1';

        var apiResponse = api.call(
            'personalize',
            host,
            requestSettings.region,
            'POST',
            arguments.path,
            { },
            headers,
            payloadString,
            requestSettings.awsCredentials
        );
        apiResponse[ 'data' ] = deserializeJSON( apiResponse.rawData );

        return apiResponse;
    }

}
